apply plugin: 'com.android.library'
apply plugin: 'maven-publish'

def githubProperties = new Properties()
githubProperties.load(new FileInputStream(rootProject.file("github.properties")))


android {
    compileSdkVersion rootProject.compileSdkVersion
    buildToolsVersion rootProject.buildToolsVersion
    defaultConfig {
        minSdkVersion 14
        targetSdkVersion rootProject.compileSdkVersion
        versionCode 1
        versionName rootProject.getVersionName
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
    lintOptions {
        abortOnError false
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId 'com.canhub.cropper'
            artifactId "Android-Image-Cropper"
            version rootProject.getVersionName
            artifact("$buildDir/outputs/aar/Android-Image-Cropper-release.aar")
        }
    }

    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/CanHub/Android-Image-Cropper")
            credentials {
                username = githubProperties['gpr.usr'] ?: System.getenv("GPR_USER")
                password = githubProperties['gpr.key'] ?: System.getenv("GPR_API_KEY")
            }
        }
    }
}

dependencies {
    api "androidx.appcompat:appcompat:$androidXLibraryVersion"
    implementation "androidx.exifinterface:exifinterface:$androidXLibraryVersion"
}

